plugins {
  id 'com.github.spotbugs' version '4.7.0'
}

dependencies {
  api 'com.ninja-squad:DbSetup:2.1.0'
  implementation 'org.apache.commons:commons-csv:1.8'
  compileOnly 'org.jetbrains:annotations:20.1.0'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
  testImplementation 'org.assertj:assertj-db:2.0.2'
  testRuntimeOnly 'com.h2database:h2:1.4.200'
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
  withJavadocJar()
}

spotbugs {
  excludeFilter.set(file("$projectDir/config/spotbugs/exclude.xml"))
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
      'Export-Package': 'com.sciencesakura.dbsetup.csv',
      'Import-Package': 'com.ninja_squad.dbsetup,'
        + 'com.ninja_squad.dbsetup.bind,'
        + 'com.ninja_squad.dbsetup.generator,'
        + 'com.ninja_squad.dbsetup.operation,'
        + 'org.apache.commons.csv'
    )
  }
}

javadoc {
  destinationDir = file("$rootDir/docs/core")
  options.noTimestamp = true
  options.links(
    'https://docs.oracle.com/javase/8/docs/api/',
    'http://dbsetup.ninja-squad.com/apidoc/2.1.0/'
  )
}

clean {
  delete javadoc.destinationDir
}
