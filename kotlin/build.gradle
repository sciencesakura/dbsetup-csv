plugins {
  id 'org.jetbrains.dokka' version '1.4.30'
  id 'org.jetbrains.kotlin.jvm' version '1.4.32'
  id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
}

dependencies {
  api project(':dbsetup-csv')
  api 'com.ninja-squad:DbSetup-kotlin:2.1.0'

  testRuntimeOnly 'com.h2database:h2:1.4.200'
  testImplementation 'org.assertj:assertj-db:2.0.2'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
      'Export-Package': 'com.sciencesakura.dbsetup.csv',
      'Import-Package': 'com.ninja_squad.dbsetup_kotlin,'
        + 'com.sciencesakura.dbsetup.csv'
        + ";bundle-symbolic-name=\"${project.group}.${project(':dbsetup-csv').name}\""
        + ";bundle-version=\"${project.version}\""
    )
  }
}

dokkaHtml {
  outputDirectory.set(file("$rootDir/docs/kotlin"))
}

dokkaJavadoc {
  outputDirectory.set(temporaryDir)
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  archiveClassifier.set('javadoc')
  from dokkaJavadoc.outputDirectory
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact kotlinSourcesJar
      artifact javadocJar
    }
  }
}

clean {
  delete dokkaHtml.outputDirectory
}
