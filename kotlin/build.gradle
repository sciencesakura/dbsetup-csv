plugins {
  id 'org.jetbrains.dokka' version '1.6.0'
  id 'org.jetbrains.kotlin.jvm' version '1.6.0'
  id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

dependencies {
  api project(':dbsetup-csv')
  api 'com.ninja-squad:DbSetup-kotlin:2.1.0'

  testRuntimeOnly 'com.h2database:h2:2.0.202'
  testImplementation 'org.assertj:assertj-db:2.0.2'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
      'Export-Package': 'com.sciencesakura.dbsetup.csv',
      'Import-Package': 'com.ninja_squad.dbsetup_kotlin,'
        + 'com.sciencesakura.dbsetup.csv'
        + ";bundle-symbolic-name=\"${project.group}.${project(':dbsetup-csv').name}\""
        + ";bundle-version=\"${project.version}\""
    )
  }
}

dokkaHtml {
  outputDirectory.set(file("$rootDir/docs/kotlin"))
}

dokkaJavadoc {
  outputDirectory.set(temporaryDir)
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  archiveClassifier.set('javadoc')
  from dokkaJavadoc.outputDirectory
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact kotlinSourcesJar
      artifact javadocJar
    }
  }
}

clean {
  delete dokkaHtml.outputDirectory
}
