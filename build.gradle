plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
}

group = 'com.sciencesakura'
archivesBaseName = 'dbsetup-csv'
version = '0.0.1'
description = 'An extension of DbSetup to import data from external CSV/TSV files.'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  implementation 'com.ninja-squad:DbSetup:2.1.0'
  implementation 'org.apache.commons:commons-csv:1.7'
  compileOnly 'org.jetbrains:annotations:17.0.0'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.powermock:powermock-module-junit4:2.0.4'
  testImplementation 'org.powermock:powermock-api-mockito2:2.0.4'
}

tasks.withType(JavaCompile) {
  options.encoding = 'utf-8'
}

javadoc {
  options.encoding = 'utf-8'
  options.locale = 'en'
  options.noTimestamp = true
  options.links(
    'https://docs.oracle.com/javase/8/docs/api/',
    'http://dbsetup.ninja-squad.com/apidoc/2.1.0/'
  )
}

tasks.withType(Jar) {
  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Vendor': project.group.split('\\.').reverse().join('.'),
      'Implementation-Version': project.version
    )
  }
  metaInf {
    from new File(projectDir, 'LICENSE')
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = project.name
        description = project.description
        url = 'https://github.com/sciencesakura/dbsetup-csv'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            name = 'sciencesakura'
            email = 'sakurasakurascience@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/sciencesakura/dbsetup-csv.git'
          developerConnection = 'scm:git:git://github.com/sciencesakura/dbsetup-csv.git'
          url = 'https://github.com/sciencesakura/dbsetup-csv'
        }
      }
    }
  }
  repositories {
    maven {
      url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username project.ossrhUsername
        password project.ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.maven
}
