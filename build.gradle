allprojects {
  group = 'com.sciencesakura'
  version = '3.0.0-SNAPSHOT'
  description = 'An extension of DbSetup to import data from CSV/TSV files.'

  repositories {
    mavenCentral()
    maven {
      // for kotlinx-html-jvm
      url = 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven'
    }
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  jar {
    manifest {
      def vendor = project.group.split('\\.').reverse().join('.')
      attributes(
        'Implementation-Title': project.name,
        'Implementation-Vendor': vendor,
        'Implementation-Version': project.version,
        'Bundle-ManifestVersion': 2,
        'Bundle-Name': project.name,
        'Bundle-SymbolicName': "${project.group}.${project.name}",
        'Bundle-Vendor': vendor,
        'Bundle-Version': project.version
      )
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java
        pom {
          name = project.name
          description = project.description
          url = 'https://github.com/sciencesakura/dbsetup-csv'
          organization {
            name = 'sciencesakura.com'
            url = 'https://www.sciencesakura.com/'
          }
          licenses {
            license {
              name = 'MIT License'
              url = 'https://opensource.org/licenses/MIT'
            }
          }
          developers {
            developer {
              name = 'sciencesakura'
              email = 'sakurasakurascience@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/sciencesakura/dbsetup-csv.git'
            developerConnection = 'scm:git:git://github.com/sciencesakura/dbsetup-csv.git'
            url = 'https://github.com/sciencesakura/dbsetup-csv'
          }
        }
      }
    }
    repositories {
      maven {
        url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        credentials {
          username project.ossrhUsername
          password project.ossrhPassword
        }
      }
    }
  }

  signing {
    sign publishing.publications.maven
  }
}
